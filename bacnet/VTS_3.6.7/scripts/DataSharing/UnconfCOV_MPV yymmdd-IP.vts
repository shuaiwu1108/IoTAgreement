  ;-------------------------------------------------------------------------------------
  ;  Unconfirmed Change of Value (COV) Notification Service Initiation Tests
  ; COV Notification from a Multi-state Input, Multi-state Output, and
  ; Multi-state Value Object Present Value Property
  ;
  ;  UnconfirmedCOV_MPV.vts UnCOVddmmyy-05.vts
  ;
  ;  Ver 0.1    03/20/01    P. Bowen    Initial untested version (no MI/MO/MV objects to test)
  ;  Ver 0.2    05/27/01    P. Bowen    Updated test step documentation
  ;                     and make LIFETIME = 0
  ;
  ;  Purpose: This test case verifies that the IUT can initiate
  ; UnconfirmedCOVNotification service requests conveying a change of the
  ; Present_Value property of Multi-state Input, Multi-state Output, and
  ; Multi-state Value objects.
  ;
  ;  Note: the following tests should be executed before running this test:
  ; ReadProperty Service Execution Tests
  ; WriteProperty Service Execution Tests
  ;
  ;  In the REFERENCE section "(B)" refers to the BACnet standard and
  ;  "(T)" refers to the testing addendum.
  ;-------------------------------------------------------------------------------------

  SETUP Unconfirmed COV Notification Test - Multi-state Object Present Value Property
  INCLUDE "..\config.vts"

  OBJECT1 = Multistate-Value, 2 ; Object which is the subject of the COV tests

  PID = 14          ; Subscriber Process Identifier
  LIFETIME = 0          ; Subscription lifetime in secs (0 = indefinite)
  WRITE_PRIORITY = 1        ; Priority for writing changes
  MSTATE_VALUE = 3      ; a new PV state which differs from the default


   SECTION Unconfirmed COV Notification Test - Multi-state Object Present Value Property

  -- This test takes place across test steps #1a - #1d so the TD can be manually
  -- paused between tests. This is necessary since the COV Notifications must be
  -- verified manually.

  -- Configuration Requirements: At the beginning of the test, the Out-Of-Service
  -- property shall have a value of FALSE.

  -- Test Concept: A subscription for COV notifications is established. The
  -- Present_Value of the monitored object is changed and a notification shall
  -- be received. The Present_Value may be changed using the WriteProperty service
  -- or by another means such as changing the input signal represented by a
  -- Multi-state Input object. For some implementations it may be necessary to
  -- write to the Out_Of_Service property first to accomplish this task. For
  -- implementations where it is not possible to write to these properties at all
  -- the vendor shall provide an alternative trigger mechanism to accomplish this
  -- task. All of these methods are equally acceptable.

  -- Test Steps:
  --    REPEAT X = (one supported object of each type from the set Multi-state Input,
  --            Multi-state Output, and Multi-state Value) DO {
  --    1. TRANSMIT SubscribeCOV-Request,
  --        'Subscriber Process Identifier' = (any value > 0 chosen by the TD),
  --        'Monitored Object Identifier' = X,
  --        'Issue Confirmed Notifications' = FALSE,
  --        'Lifetime' = 0
  --    2. RECEIVE BACnet-SimpleACK-PDU
  --    3. RECEIVE UnconfirmedCOVNotification-Request,
  --        'Subscriber Process Identifier' = (the same value used in step 1),
  --        'Initiating Device Identifier' = IUT,
  --        'Monitored Object Identifier' = X,
  --        'Time Remaining' = 0,
  --        'List of Values' = (the initial Present_Value and initial Status_Flags)
  --    4. IF (Out_Of_Service is writable) THEN
  --        WRITE X, Out_Of_Service = TRUE
  --       RECEIVE BACnet-SimpleACK-PDU
  --    5. BEFORE Notification Fail Time
  --         RECEIVE UnconfirmedCOVNotification-Request,
  --            'Subscriber Process Identifier' = (the same value used in step 1),
  --            'Initiating Device Identifier' = IUT,
  --            'Monitored Object Identifier' = X,
  --            'Time Remaining' = 0,
  --            'List of Values' = (the initial Present_Value and new Status_Flags)
  --    6. IF (Present_Value is now writable) THEN
  --        WRITE X, Present_Value = (any value that differs from "initial
  --        Present_Value")
  --        RECEIVE BACnet-SimpleACK-PDU
  --       ELSE
  --        MAKE (Present_Value = any value that differs from "initial
  --        Present_Value")
  --    7. BEFORE NotificationFailTime
  --        RECEIVE UnconfirmedCOVNotification-Request,
  --        'Subscriber Process Identifier' = (the same value used in step 1),
  --        'Initiating Device Identifier' = IUT,
  --        'Monitored Object Identifier' = X,
  --        'Time Remaining' = 0,
  --        'List of Values' = (the new Present_Value and new Status_Flags)
  --    8. TRANSMIT SubscribeCOV-Request,
  --        'Subscriber Process Identifier' = (the same value used in step 1),
  --        'Monitored Object Identifier' = X
  --    9. RECEIVE BACnet-SimpleACK-PDU
  --    10. IF (Out_Of_Service is writable) THEN
  --        WRITE X, Out_Of_Service = FALSE
  --        RECEIVE BACnet-SimpleACK-PDU
  --    }

  ;-------------------------------------------------------------------------------------
  TEST #1a Subscribe to COV and Receive Unconfirmed COV Notification

  DEPENDENCIES None

  REFERENCE (B) Unconfirmed COV Notification Service and (T) Unconfirmed COV Notification Test - MI/MO/MV PV Property

  -- Steps #1 - 2: Subscribe to COV and Receive Simple ACK

    SEND (
      Da = IUT_ADDR
      PDU = Confirmed-Request
      Service = SubscribeCOV
      Unsigned = 0, PID         ; Process-Identifier = PID
      Object = 1, OBJECT1       ; Object-Identifier = OBJECT1
      Boolean = 2, FALSE        ; Request unconfirmed notifications
      Unsigned = 3, LIFETIME        ; Subscribe for LIFETIME seconds
    )

    EXPECT (
      Sa = IUT_ADDR
      PDU = SimpleAck
      Service = SubscribeCOV
    )

  -- Step #3: Receive Unconfirmed COV Notification Request

  --  AND (             ; Can't control the timing of the IUT's
  --    PDU = Confirmed-Request
  --    Service = UnconfirmedCOVNotification
  --  )

  ;-------------------------------------------------------------------------------------
  TEST #1b Make Out-of-Service = TRUE and Receive Unconfirmed COV Notification Request

  DEPENDENCIES None

  -- Step #4: Make Out-of-Service = TRUE and Receive Simple ACK

   SEND (
      Da = IUT_ADDR
      PDU = Confirmed-Request
      Service = WriteProperty
      Object = 0, OBJECT1
      Property = 1, Out-of-Service
      OpenTag 3
        BOOLEAN = TRUE          ; Make Out-of-Service = TRUE
      CloseTag 3
      Unsigned = 4, WRITE_PRIORITY  ; Set Out-of-Service write priority
    )

    EXPECT (
      Sa = IUT_ADDR
      PDU = SimpleAck
      Service = WriteProperty
    )

  -- Step #5: Before NotificationFailTime Receive Unconfirmed COV Notification Request

  --  AND (             ; Can't control the timing of the IUT's
  --    PDU = Confirmed-Request
  --    Service = UnconfirmedCOVNotification
  --  )

  ;-------------------------------------------------------------------------------------
  TEST #1c  Make PV = any value that differs from "Initial Present_Value"

  DEPENDENCIES None

  -- Step #6: PV = any value that differs from "Initial PV" and Receive Simple ACK

   SEND (
      Da = IUT_ADDR
      PDU = Confirmed-Request
      Service = WriteProperty
      Object = 0, OBJECT1
      Property = 1, Present-Value
      OpenTag 3
        Unsigned = MSTATE_VALUE     ; One of N states, N is defined in EPICS
      CloseTag 3
      Unsigned = 4, WRITE_PRIORITY  ; Set PV write priority
    )

    EXPECT (
      Sa = IUT_ADDR
      PDU = SimpleAck
      Service = WriteProperty
    )

  -- Step #7: Before NotificationFailTime Receive Unconfirmed COV Notification Request

  --  AND (             ; Can't control the timing of the SubscribeCOV
  --    PDU = Confirmed-Request
  --    Service = UnconfirmedCOVNotification
  --  )

 ;-------------------------------------------------------------------------------------
  TEST #1d Send Unsubscribe COV Request and Make Out-of-Service = FALSE

  DEPENDENCIES None

  -- Steps #8 & 9: Send Unsubscribe COV Request to IUT and Receive Simple ACK

    SEND (
      Da = IUT_ADDR
      PDU = Confirmed-Request
      Service = SubscribeCOV        ; Cancel COV Subscription request
      Unsigned = 0, PID         ; Process-Identifier = PID
      Object = 1, OBJECT1       ; Object-Identifier = OBJECT1
    )

    EXPECT (
      Sa = IUT_ADDR
      PDU = SimpleAck
      Service = SubscribeCOV
    )

  -- Step #10: Return point to service (make Out-of-Service = FALSE)

   SEND (
      Da = IUT_ADDR
      PDU = Confirmed-Request
      Service = WriteProperty
      Object = 0, OBJECT1
      Property = 1, Out-of-Service
      OpenTag 3
         BOOLEAN = FALSE        ; Make Out-of-Service = FALSE
      CloseTag 3
      Unsigned = 4, WRITE_PRIORITY  ; Set Out-of-Service write priority
    )

    EXPECT (
      Sa = IUT_ADDR
      PDU = SimpleAck
      Service = WriteProperty
    )